import java.nio.file.Paths
import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

buildscript {
    ext {
        kotlin_version = "2.3.0"
        kotlinx_coroutines_version = "1.10.2"
        classgraph_version = "4.8.112"
        imgui_version = "1.87.6"
        gson_version = "2.8.9"
        lwjgl_version = "3.3.2"
        mackjpeg_version = "1.0.5"

        eocvsim_version = '4.1.0-dev-260128-2124'

        slf4j_version = "2.0.16"
        log4j_version = "2.24.1"
        logback_classic_version = "1.5.13"

        Penv = findProperty('env')
        if(Penv != null && (Penv != 'dev' && Penv != 'release')) {\
            throw new GradleException("Invalid env property, must be 'dev' or 'release'")
        }

        env = Penv == 'release' ? 'release' : 'dev'

        println("Current build is: $env")
    }

    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.30.0'
    }
}

allprojects {
    group 'org.deltacv.PaperVision'
    version '1.0.10'

    ext {
        standardVersion = version
    }

    repositories {
        mavenCentral()
        mavenLocal()
        google()
        maven { url "https://maven.pkg.jetbrains.space/public/p/compose/dev" }
        maven { url 'https://artifacts.openmicroscopy.org/artifactory/ome.releases/' }
    }

    if(env == 'dev') {
        String date = DateTimeFormatter.ofPattern(
                "yyMMdd-HHmm"
        ).format(LocalDateTime.now())

        String hash = findProperty('hash')

        version += "-dev-${hash ?: date}"
        println("Final version of ${project} is $version")

        File libsFolder = Paths.get(
                projectDir.absolutePath, 'build', 'libs'
        ).toFile()

        for(file in libsFolder.listFiles()) {
            if(file.name.contains("dev") && file.name.endsWith(".jar"))
                file.delete()
        }
    }
}
